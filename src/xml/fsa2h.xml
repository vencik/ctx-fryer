<?xml version="1.0" encoding="utf-8"?>
<!DOCTYPE xsl:stylesheet [
<!ENTITY dquote "&#34;">
<!ENTITY quote "&#39;">
]>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:ctx-fryer="http://127.0.0.1/project"
                version="1.0">

<xsl:output method="text" encoding="ISO-8859-1" />
<xsl:strip-space elements="*" />

<!-- Pad string with spaces (from right) to defined length -->
<xsl:template name="pad-string">
  <xsl:param name="string"   data-type="string" />
  <xsl:param name="length"   data-type="number" />
  <xsl:param name="pad-at"   data-type="string" select="'left'" />
  <xsl:param name="pad-char" data-type="string" select="' '" />
  <xsl:variable name="to-go" data-type="number" select="$length - string-length($string)" />
  <xsl:choose>
    <xsl:when test="$to-go &lt;= 0">
      <xsl:value-of select="$string" />
    </xsl:when>
    <xsl:otherwise>
      <xsl:variable name="padded-string">
        <xsl:choose>
          <xsl:when test="$pad-at = 'left'">
            <xsl:value-of select="concat($pad-char, $string)" />
          </xsl:when>
          <xsl:when test="$pad-at = 'right'">
            <xsl:value-of select="concat($string, $pad-char)" />
          </xsl:when>
          <xsl:otherwise>
            <!-- LOGICAL ERROR (but how can it be propagated?) -->
            <xsl:value-of select="'INTERNAL ERROR'" />
          </xsl:otherwise>
        </xsl:choose>
      </xsl:variable>
      <xsl:call-template name="pad-string">
        <xsl:with-param name="string"   select="$padded-string" />
        <xsl:with-param name="length"   select="$length" />
        <xsl:with-param name="pad-at"   select="$pad-at" />
        <xsl:with-param name="pad-char" select="$pad-char" />
      </xsl:call-template>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>


<!-- Maximal node text string length -->
<xsl:template name="max-string-length">
  <xsl:param name="nodes" />
  <xsl:param name="min" data-type="number" select="0" />
  <xsl:choose>
    <xsl:when test="count($nodes) = 0">
      <xsl:value-of select="$min" />
    </xsl:when>
    <xsl:when test="count($nodes) = 1">
      <xsl:value-of select="string-length(string($nodes[position() = 1]))" />
    </xsl:when>
    <xsl:otherwise>
      <xsl:variable name="even-maxlen">
        <xsl:call-template name="max-string-length">
          <xsl:with-param name="nodes" select="$nodes[position() mod 2 = 0]" />
        </xsl:call-template>
      </xsl:variable>
      <xsl:variable name="odd-maxlen">
        <xsl:call-template name="max-string-length">
          <xsl:with-param name="nodes" select="$nodes[position() mod 2 != 0]" />
        </xsl:call-template>
      </xsl:variable>
      <xsl:choose>
        <xsl:when test="$even-maxlen &gt; $odd-maxlen">
          <xsl:value-of select="$even-maxlen" />
        </xsl:when>
        <xsl:otherwise>
          <xsl:value-of select="$odd-maxlen" />
        </xsl:otherwise>
      </xsl:choose>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:variable name="terminal-symbols" select="/lr-parser/grammar/terminals/list/list-item/terminal-symbol" />
<xsl:variable name="terminal-ident-pad-length">
  <xsl:call-template name="max-string-length">
    <xsl:with-param name="nodes" select="$terminal-symbols/@identifier" />
  </xsl:call-template>
</xsl:variable>
<xsl:variable name="item-code-pad-length" select="string-length(string(count($terminal-symbols))) + 1" />


<xsl:template match="lr-parser">#ifndef lexical_items_h
#define lexical_items_h

/**
 *  \brief   FSA <xsl:value-of select="fsa_id" />: Accepted lexical items
 *
 *  The code is generated; do NOT change it, manually.
 *
 *  This file is part of code generated by CTXFryer
 *  while producing lexical analyser source code.
 *
 *  \date  2012/06/15
 */

<!-- Accepted lexical items -->
<xsl:for-each select="$terminal-symbols">
  <xsl:sort select="@id" />
#define LEXI_<xsl:call-template name="pad-string">
    <xsl:with-param name="string" select="@identifier" />
    <xsl:with-param name="length" select="$terminal-ident-pad-length" />
    <xsl:with-param name="pad-at" select="'right'" />
  </xsl:call-template>
  <xsl:call-template name="pad-string">
    <xsl:with-param name="string" select="position()" />
    <xsl:with-param name="length" select="$item-code-pad-length" />
  </xsl:call-template>  /**&lt; Lexical item for <xsl:call-template name="pad-string">
    <xsl:with-param name="string" select="concat('&dquote;', @identifier, '&dquote;')"  />
    <xsl:with-param name="length" select="$terminal-ident-pad-length + 2" />
    <xsl:with-param name="pad-at" select="'right'" />
  </xsl:call-template>
  <xsl:text> terminal symbol */</xsl:text>
</xsl:for-each>

/** Number of lexical items (including EoF) */
#define LEXICNT <xsl:value-of select="count($terminal-symbols) + 1" />

#endif /* end of #ifndef lexical_items_h */
</xsl:template>

</xsl:stylesheet>
